% --- Box containing bunny:

surf 1 cyl 0.0 0.0 17.0 -0.1 27

cell 1 0 fill 1  -1 % Fill box with mesh based universe
cell 2 0 outside  1 % Outside of the box is outside

% --- Create the background universe (filled with void)

surf 3 inf
cell 4 2 air -3

% --- Create the STL-bunny

solid 2 1 2                        % type, universe, bacground universe
10 4 5 4 3 2                       % search mesh split, depth and sizes
1 1E-6                             % ray test, merge radius
body fuel fuel fuel                % solid name, cell name, material name
file fuel "bunny.stl" 0.20 0 0 0   % body name, file, scaling factor, origin

% --- Composition for bunny:

mat fuel  4.7984E-02 rgb 222 222 222
92235.03c 4.4994E-02
92238.03c 2.4984E-03
92234.03c 4.9184E-04

mat air 1E-18 rgb 200 250 200
2004.03c 1.0

% --- Criticality source simulation:

set pop 20000 500 20
set gcu -1

% --- Geometry and mesh plots

plot 1 500 399
plot 2 500 399
plot 3 500 500

% --- Fission rate detector:

det 1 dr -6 void

% Fission rate mesh plot:

mesh 8 4 1 1 500 399
mesh 8 4 1 2 500 399
mesh 8 4 1 3 500 500

set acelib "sss_jeff31u.xsdata"
