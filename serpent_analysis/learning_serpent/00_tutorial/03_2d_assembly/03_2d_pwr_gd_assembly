% --- SERPENT2 Tutorial ---

set title "Tutorial 3 - 2D PWR assembly model (infinite lattice)"

% --- Cross section library file path:

set acelib "/home/stalex/serpent/xsdata/sss_endfb7u.xsdata"
set declib "/home/stalex/serpent/xsdata/sss_endfb7.dec"
set nfylib "/home/stalex/serpent/xsdata/sss_endfb7.nfy"

% --- Define thermal scattering libraries associated with hydrogen in light water
%     As there are no readymade thermal scattering libraries for 583 K
%     We will tell Serpent to interpolate using two bounding libraries:
%     -lwj3.11t (H-1 in light water at 574 K)
%     -lwj3.13t (H-1 in light water at 624 K)
%     See also: http://montecarlo.vtt.fi/download/SSS_THERMAL.pdf

therm lwtr 583 lwj3.11t lwj3.13t

/************************
 * Material definitions *
 ************************/

% --- 3.0 wt-% enriched UO2
%     Temperature is set to 950 K

mat fuelNoGad  -10.3070 tmp 950.0 rgb 255 255 150
92235.09c       -0.02644492
92238.09c       -0.85505247
 8016.09c       -0.11850261

% --- 0.25 wt-% enriched UO2 with 8.0 wt-% of Gd2O3
%     Temperature is set to 650 K

mat fuelYesGad -10.3070 tmp 650.0 rgb 150 255 150
92235.06c       -0.00202753
92238.06c       -0.80898345
 8016.06c       -0.11957908
64152.06c       -0.00013411
64154.06c       -0.00148108
64155.06c       -0.01012049
64156.06c       -0.01408805
64157.06c       -0.01083999
64158.06c       -0.01731511
64160.06c       -0.01543111

% --- Cladding material Zircaloy-4
%     [Composition from PNNL-15870, Rev. 1]

mat Zircaloy4 -6.56000E+00 tmp 610
 8016.03c  -1.19276E-03
24050.03c  -4.16117E-05
24052.03c  -8.34483E-04
24053.03c  -9.64457E-05
24054.03c  -2.44600E-05
26054.03c  -1.12572E-04
26056.03c  -1.83252E-03
26057.03c  -4.30778E-05
26058.03c  -5.83334E-06
40090.03c  -4.97862E-01
40091.03c  -1.09780E-01
40092.03c  -1.69646E-01
40094.03c  -1.75665E-01
40096.03c  -2.89038E-02
50112.03c  -1.27604E-04
50114.03c  -8.83732E-05
50115.03c  -4.59255E-05
50116.03c  -1.98105E-03
50117.03c  -1.05543E-03
50118.03c  -3.35688E-03
50119.03c  -1.20069E-03
50120.03c  -4.59220E-03
50122.03c  -6.63497E-04
50124.03c  -8.43355E-04

% --- Coolant is water with 650 ppm soluble boric acid added
%     The temperature of water is 583 K
%     Density is calculated based on a pressure of 15.5 MPa
%     Hydrogen is flagged as a bound scatterer with the "moder"-card

mat water -0.70602 tmp 583 moder lwtr 1001 rgb 200 200 255
O-16.03c   3.330861e-01
H-1.03c    6.663259e-01
B-10.03c   7.186970e-05
B-11.03c   2.892846e-04



/************************
 * Geometry definitions *
 ************************/

% --- Normal fuel rod (no gadolinia in fuel)
pin FF
fuelNoGad  0.3975
void       0.4125
Zircaloy4  0.4750
water

% --- Gadolinium fuel rod
pin GG
fuelYesGad  0.3975
void        0.4125
Zircaloy4   0.4750
water

% --- Empty instrumentation thimble
pin ii
water       0.5725
Zircaloy4   0.6125
water

% --- Empty control rod channel
pin cc
water       0.5725
Zircaloy4   0.6125
water

% --- Empty lattice position (just water)
pin ww
water

% --- Pin lattice definition, name of the lattice "lat1"
%     Lattice type 1 (square lattice)
%     Lattice centered at 0.0 0.0
%     19 x 19 lattice elements (17x17 fuel rods + 1 layer of water)
%     Lattice pitch 1.26 cm
%     We'll utilize our knowledge of the 1/8 symmetry so that
%     we'll only have to type in 1/8th of the assembly

lat lat1 1 0.0 0.0 19 19 1.26
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww FF FF FF FF FF FF FF FF FF ww
ww ww ww ww ww ww ww ww ww FF FF FF FF FF FF FF FF ww ww
ww ww ww ww ww ww ww ww ww cc FF FF cc FF FF GG ww ww ww
ww ww ww ww ww ww ww ww ww FF GG FF FF FF cc ww ww ww ww
ww ww ww ww ww ww ww ww ww FF FF FF FF GG ww ww ww ww ww
ww ww ww ww ww ww ww ww ww cc FF FF cc ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww FF FF GG ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww FF FF ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ii ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww

% --- Tell Serpent to apply a symmetry to the lattice "lat1"
%     Symmetry axis is the z-axis (3)
%     Symmetry boundary condition is reflective (2)
%     Symmetry axis is located at coordinates (0.0 0.0)
%     Symmetry covers an angle starting from 270 degrees
%     and extending for 45 degrees

set usym lat1 3 2 0.0 0.0 270 45

% --- A square surface around the fuel assembly

surf s1 sqc  0.0 0.0 10.752

% --- Cell c1 belongs to the base universe 0, is filled with the lattice lat1
%     and covers everything inside surface s1

cell c1 0 fill lat1     -s1

% --- Cell c2 belongs to the base universe 0, is defined as an "outside" cell
%     and covers everything outside surface s1

cell c2 0 outside        s1

/******************
 * Run parameters *
 ******************/

% --- Assembly linear power is 420 kW

set power 42000

% --- Boundary condition (1 = black, 2 = reflective, 3 = periodic)

set bc 2

% --- Neutron population: 10000 neutrons per cycle, 100 active / 20 inactive cycles

set pop 15000 1000 20

/******************
     * Plots *
 ******************/

% --- XY-plot (3), which is 700 by 700 pixels and covers the whole geometry

plot 3 700 700

% --- XY-meshplot (3), which is 700 by 700 pixels and covers the whole geometry

mesh 3 700 700

/******************
   * Detectors *
 ******************/

% --- Detector that calculates the pin power distribution in the assembly: 
%     Name of the detector is pinpowers.
%     The detector uses response number -8 (fission heat deposition)
%     of the material at the interaction site (keyword: void).
%     The detector has 17 bins in the x-direction between -10.71 cm and 10.71 cm
%     The detector has 17 bins in the y-direction between -10.71 cm and 10.71 cm

det pinpowers dr -8 void dx -10.71 10.71 17 dy -10.71 10.71 17


% --- Detector that calculates the radial fission distribution in the fuel: 
%     Name of the detector is RadialFission.
%     The detector uses response number -6 (total fission cross section)
%     of the material at the interaction site (keyword: void)
%     Detector only scores interactions at material "fuel" ("dm fuel").
%     Detector calculates the spatial distribution using a cylindrical mesh "dn 1"
%     with 20 bins in the radial direction between 0.0 and 0.4025 cm ("0.0 0.4025 20")
%     with 1 bin in the angular direction between 0 and 360 degrees ("0 360 1")
%     with 1 bin in the axial direction that extends from -infinity to infinity ("0 0 1")

%det RadialFission dr -6 void dm fuelNoGad dn 1 0.0 0.4025 20 0 360 1 0 0 1